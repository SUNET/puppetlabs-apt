<%- | $name, $pin_release, $release, $codename, $release_version, $component, $originator, $label, $version, $origin, $explanation, $packages_string, $priority | -%>
<%-
$pin =
if $pin_release != '' {
  $options = [
    if $release != ''         { "a=${release}" },
    if $codename != ''        { "n=${codename}" },
    if $release_version != '' { "v=${release_version}"},
    if $component != ''       { "c=${component}" },
    if $originator != ''      { "o=${originator}" },
    if $label != ''           { "l=${label}" },
    ].filter |$x| { $x != undef }
   "release ${options.join(', ')}" }

elsif $version {
   "version ${version}" }
elsif $origin {
   "origin ${origin}" }
else {
  "release a=${name}" #Default value
}
-%>
Explanation: <%= $explanation %>
Package: <%= $packages_string %>
Pin: <%= $pin %>
Pin-Priority: <%= $priority %>
